# 硬盘读写

因为MBR实际上可写的代码只有446个字节，太少了，根本装不下内核，因此我们需要在MBR里面写入读磁盘的代码，把**内核加载器**的代码从硬盘**读到内存里**。

硬盘是外设，因此需要通过`in out`指令操作外设的**端口**，这里我们就实现对于硬盘的读写操作（主要是读）。

* 端口是**外设**的**寄存器**，这个端口和网络协议栈里的套接字端口完全是不同的概念，这里是正儿八经的**寄存器**

## 读写模式
* CHS模式：
    典中典的柱面、磁头、扇区的寻址方式，但是太麻烦了，还得给三维坐标

* LBA模式：
    逻辑块地址模式，不用关心具体坐标，我们用的就是这种模式。

    我们用的LBA28，总共可以访问128G的磁盘空间。

* 端口：
  * 0x1F0: 16位端口，用于读写数据
  * 0x1F1: 检测前一个指令的错误
  * 0X1F2: 读写扇区的数量
  * 0X1F3: 起始扇区的0~7位
  * 0X1F4: 起始扇区的8~15位
  * 0X1F5: 起始扇区的16~23位
  * 0X1F6:
    * 0-3: 起始扇区的23 ~ 27位 
    * 4: 0主盘 1从盘
    * 6: 0CHS模式 1LBA模式
    * 5 7: 固定为1
  * 0x1F7: 用于out时控制磁盘动作
    * 0xEC: 识别硬盘
    * 0x20: 读硬盘
    * 0x30: 写硬盘
  * 0x1F7: 用于in时读取磁盘状态
    * 0: ERR
    * 3: DRQ数据准备完毕
    * 7: BSY硬盘繁忙

下面实现的是从第0个扇区**开始**读**1个**扇区到内存`0x1000`的位置，也就是MBR**自己读自己**。

因此读完之后，0x1000和0x7c00处的东西一模一样，都是MBR🤭
```s
mov edi, 0x1000; 读取到内存0x1000的位置
mov ecx, 0; 起始扇区
mov bl, 1; 读的扇区数量
call read_disk

read_disk:
    ;设置读写扇区的数量
    mov dx, 0x1f2
    mov al, bl
    out dx, al

    inc dx; 0x1f3
    mov al, cl ;起始扇区的前8位，ecx里第0个扇区因此实际上全是0
    out dx, al

    inc dx; 0x1f4
    shr ecx, 8
    mov al, cl ;起始扇区的中8位
    out dx, al

    inc dx; 0x1f5
    shr ecx, 8
    mov al, cl ;起始扇区的高8位
    out dx, al

    inc dx; 0x1f6
    shr ecx, 8
    and cl, 0b1111 ;将高4位置为0

    mov al, 0b1110_0000
    or al, cl
    out dx, al ;主盘 LBA模式

    inc dx; 0x1f7
    mov al, 0x20 ;0x1f7为控制端口，0x20表示读硬盘操作
    out dx, al

    xor ecx, ecx; 清空ecx
    mov cl, dl; 得到读写扇区的数量
    .read:
        push cx
        call .waits ;等待数据准备完毕
        call .reads
        pop cx
        loop .read ;读取一个扇区

    ret
    .waits:
        mov dx, 0x1f7
        .check:
            in al, dx
            jmp $+2 ;一点延迟
            jmp $+2
            jmp $+2
            and al, 0b1000_1000
            cmp al, 0b0000_1000
            jnz .check
        ret

    .reads:
        mov dx, 0x1f0
        mov cx, 256; 一个扇区256个word
        .readw:
            in ax, dx
            jmp $+2 ;一点延迟
            jmp $+2
            jmp $+2
            mov [edi], ax;移动到目标内存0x1000
            add edi, 2 ;读一个字到内存后edi位置后移2
            loop .readw
        ret

```

