# 保护模式和全局描述符

之前的8086实模式，那时候的段寄存器是为了弥补16bit的CPU无法访问1M(20bit)的内存空间**而引入的**，也就是典中典的：
> 段地址(16bit) << 4 + 偏移地址(16bit) ---> 得到20bit的地址

来提供缺省的4位地址线。并且引入了**地址回绕**这种烂活来避免地址越界。

但是后来的80286和80386不需要这种烂活了，CPU的线宽足够了，并且内存也够大了。但是段寄存器这种东西还得保留，毕竟要后向兼容，于是又引入了A20总线。

并且给段寄存器赋予了提供了更强大的功能，里面不再存段基址了，而是GDT表的索引🤭提供了特权级检验等等好玩意。


## 保护模式

保护模式是为了**保护信息**的，信息在计算机中存在于：
* 寄存器：一些寄存器只能由内核访问
* 高速缓存：对程序员透明，程序员完全无法控制的东西
* 内存 / 描述符：一些内存只能让操作系统使用，比如内核代码和内核数据等等
* 外设：硬盘/显示器等等，in/out等**特权指令**也只能由内核运行

这里主要讲如何**保护内存**，也就是**全局描述符**的作用。

## 全局描述符

用来描述一段内存的，也就是所谓的**段**。

之前内存检测得到的ARDS也是用来描述内存的，它有几个字段：
* 内存起始位置
* 内存长度
* 内存类型

全局描述符也类似：
* 段基址
* 段界限
* 段属性

具体字段如下：
```cpp
typedef struct descriptor /* 共 8 个字节 */
{
    unsigned short limit_low;      // 段界限 0 ~ 15 位
    unsigned int base_low : 24;    // 基地址 0 ~ 23 位 16M
    unsigned char type : 4;        // 段类型
    unsigned char segment : 1;     // 1 表示代码段或数据段，0 表示系统段
    unsigned char DPL : 2;         // Descriptor Privilege Level 描述符特权等级 0 ~ 3
    unsigned char present : 1;     // 存在位，1 在内存中，0 在磁盘上
    unsigned char limit_high : 4;  // 段界限 16 ~ 19;
    unsigned char available : 1;   // 该安排的都安排了，送给操作系统吧
    unsigned char long_mode : 1;   // 64 位扩展标志
    unsigned char big : 1;         // 32 位 还是 16 位;
    unsigned char granularity : 1; // 粒度 4KB 或 1B
    unsigned char base_high;       // 基地址 24 ~ 31 位
} __attribute__((packed)) descriptor;
```

这是80386的描述符，是64位的，为什么这么凌乱？是因为要兼容80286的段描述布局。


### type segment = 1

下面说明一下type的字段，总共4位：

| X | C/E | R/W | A |

- A: Accessed 是否被 CPU 访问过
- X: 1/代码段 0/数据段
- X = 1：代码段
    - C: 是否是依从代码段，若是则执行时不需要切换特权级
    - R: 是否可读
- X = 0: 数据段
    - E: 0 向上扩展 / 1 向下扩展
    - W: 是否可写

那么**栈段**呢？栈段其实就是个**数据段**，即`X == 0`，但是是向下拓展的，即`E == 1`。

## 全局描述符表 GDT

一般就是用**数组**表示了，一段连续的内存，不为啥，就为了下标索引快速，而且稳定😋


```cpp
descriptor gdt[8192];
```

可用的全局描述符有8191个，第0个为dummy。
### gdtr寄存器

CPU提供了gdtr寄存器来得到全局描述符表的**起始位置**和**长度**。

* 相关命令有：
```s
lgdt [gdt_ptr]; 加载 gdt
sgdt [gdt_ptr]; 保存 gdt
```

```cpp
typedef struct pointer
{
    unsigned short limit; // size - 1
    unsigned int base;
} __attribute__((packed)) pointer;
```

## 段选择子

我们已经知道，保护模式下段寄存器不再存段基址，而是**段选择子**：
```cpp
typedef struct selector
{
    unsigned char RPL : 2; // Request PL 
    unsigned char TI : 1; // 0  全局描述符 1 局部描述符 LDT Local 
    unsigned short index : 13; // 全局描述符表索引
} __attribute__((packed)) selector;
```
* index是GDT表的索引下标
* RPL 请求特权级
* TI 决定在全局描述符表中找还是局部描述符表LDT中找描述符

一个程序运行时，需要：
* **只需要一个**代码段: cs
* 需要一个或多个数据段: 栈段ss / 数据段ds
  * ds是**默认的**数据段，当然es / fs / gs是三个拓展
* 将需要的段选择子**加载到对应的段寄存器**中，并且可以**校验特权级**。

RPL到底有啥用？

比如当前程序运行时，此时段寄存器CS的RPL代表CPL(当前特权级)，如果是0表示当前进程**运行的是内核代码**，也就是内核态；如果是0则**运行的是用户代码**，也就是用户态。

RPL是在段选择子加载到段寄存器中，运行的某一刻时才有意义。不像DPL，DPL表示一段确定的代码，要么0要么3，请求执行那里的代码时会对比CPL和DPL的值，来确定特权级是否发生了变化。

## A20总线

正如之前所说，80286有24根地址线，可以访问16M内存；80386有32根地址线，可以访问4G内存。

完全不用地址回绕了😋因为内存足够大，A20总线首先关闭，仍然是8086的实模式；开启之后，就进入了保护模式，不再用段基址右移四位的烂活寻址方式，而是过上了有GDT表的高贵生活；也不再用地址回绕这种穷酸东西，而是大大方方访问1M以上的内存🤭


* 通过`0x92`端口开启A20总线

## cr0寄存器

启动保护模式的**标志**是，`cr0`控制寄存器的`PE`位置1。

## 实现
进入保护模式的完整过程：
* 手动写好GDT表以及表指针gdt_ptr，和**段选择子**，方便日后加载
* 关中断：cli
* 开启A20总线
* 加载gdt, lgdt [gdt_ptr]
* 开启cr0的保护模式，PE位置1
* 修改段寄存器成实现写好的段选择子
  * CS通过jmp指令修改
  * 其余通过mov修改

这之后，我们就可以访问1M以上的内存了，下面的实现一共设置了三个全局描述符：dummy，代码段和数据段，**起始地址均为0**，这和Linux的做法是一致的，这样访问起来非常方便。

```s
[org 0x1000]

; 手写一个简单的loader内核加载器
dw 0x55aa ; 自定义一个魔数嘻嘻

mov si, loading
call print

; xchg bx, bx
detect_memory:
    xor ebx, ebx;
    
    mov ax, 0
    mov es, ax ;段寄存器不能直接改
    mov edi, ards_buffer ;结果存放位置，就在下文loader靠后的位置

    mov edx, 0x534d4150; 固定签名
.next:
    mov eax, 0xe820 ;子功能号
    mov ecx, 20 ;ARDS的大小
    int 0x15; BIOS中断

    jc error ;CF置位表示出错

    add di, cx
    inc word [ards_count]; 结构体数量加一
    cmp ebx, 0
    jnz .next

    mov si, detecting ;检测结束
    call print

    jmp prepare_protected_mode; 检测内存完成之后开启保护模式
 
prepare_protected_mode:
    xchg bx, bx
    cli; 关中断
    ;开启A20总线
    in al, 0x92
    or al, 0b10
    out 0x92, al
    ;加载GDT
    lgdt [gdt_ptr]
    ;启动保护模式
    mov eax, cr0
    or eax, 1
    mov cr0, eax
    ;通过jmp指令修改CS段寄存器成选择子,CS只能这样修改
    jmp dword code_selector:protected_mode

print:
    mov ah, 0x0e
.next:
    mov al, [si]
    cmp al, 0
    jz .done
    int 0x10
    inc si
    jmp .next
.done:
    ret
loading:
    db "Loading MyOS...", 10, 13, 0 ;最后三个数字依次代表/n /r /0的ASCII码
detecting:
    db "Detecting Memory Success...", 10, 13, 0 ;最后三个数字依次代表/n /r /0的ASCII码
    
error:
    mov si, .msg
    call print
    hlt ;让CPU停止
    jmp $ ;阻塞
    .msg: db "Loading Error!!!" , 10, 13, 0


[bits 32]
protected_mode: ;保护模式代码
    ;修改剩下的段寄存器成选择子
    xchg bx, bx
    mov ax, data_selector
    mov ds, ax
    mov es, ax
    mov fs, ax
    mov gs, ax
    mov ss, ax ;初始化段寄存器

    mov esp, 0x10000 ;修改栈顶

    mov byte [0xb8000], 'p' ;直接使用1M以外的内存

jmp $

memory_base equ 0 ;内存基地址
memory_limit equ ((1024 * 1024 * 1024 * 4) / (1024*4)) - 1 ;内存界限4G/4K - 1
code_selector equ(1<<3) ;代码段选择子
data_selector equ(2<<3) ;数据段选择子

gdt_ptr: ;GDT指针
    dw (gdt_end - gdt_base) - 1
    dd gdt_base

gdt_base: ;设置GDT 3个描述符
    dd 0, 0 ;第0个dummy描述符
gdt_code: ;代码段
    dw memory_limit & 0xffff ;段界限0-15位
    dw memory_base & 0xffff; 基地址0-16位
    db (memory_base >> 16) & 0xff ; 基地址17-23位
    db 0b_1_00_1_1_0_1_0; 存在 dpl=0 代码段 非依从 未访问
    db 0b1_1_0_0_0000 | (memory_limit >> 16)&0xf; 4k 32位 不是64位 段界限
    db (memory_base >> 24) & 0xff; 段基址
gdt_data: ;数据段
    dw memory_limit & 0xffff ;段界限0-15位
    dw memory_base & 0xffff; 基地址0-16位
    db (memory_base >> 16) & 0xff ; 基地址17-23位
    db 0b_1_00_1_0_0_1_0; 存在 dpl=0 数据段 向上 可写
    db 0b1_1_0_0_0000 | (memory_limit >> 16)&0xf; 4k 32位 不是64位 段界限
    db (memory_base >> 24) & 0xff; 段基址    
gdt_end:

ards_count:
    dw 0

ards_buffer:
```
